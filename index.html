<!DOCTYPE html>
<html lang="en" >
<head>
  <meta charset="UTF-8">
  <title>CodePen - Font Experiment | WebGL Fragment Shader</title>
  <link rel="stylesheet" href="./style.css">

</head>
<body>
<!-- partial:index.partial.html -->
<p><canvas id="canvas"></canvas></p>
<!-- VertexShader code here -->
<script id="vertexShader" type="x-shader/x-vertex">#version 300 es
precision highp float;
  in vec4 position;
  void main() {
    gl_Position = vec4( position );
  }
</script>
<!-- FragmentShader code here -->
<script id="fragmentShader" type="x-shader/x-fragment">#version 300 es
precision highp float;
out vec4 fragColor;

uniform vec4 u_mouse;
uniform vec2 u_resolution;
uniform float u_time;

#define R           u_resolution
#define T           u_time
#define M           u_mouse

#define PI      3.14159265358
#define PI2     6.28318530718

#define MIN_DIST 1e-4
#define MAX_DIST 30.

const float sz = .425;
const float dz = sz*2.5;
const float df = dz/2.;
const float hf = sz/2.;
  
vec3 hit,hp;
vec2 fd,sd;

mat2 rot(float a) { return mat2(cos(a),sin(a),-sin(a),cos(a)); }
float hash21( vec2 p ) { return fract(sin(dot(p,vec2(23.43,84.21))) *4832.3234); }
float opx( in vec3 p, float d, in float h ) {
    vec2 w = vec2( d,abs(p.z)-h); return min(max(w.x,w.y),0.0) + length(max(w,0.0));
}

float box( in vec2 p, in vec2 b ){
    vec2 d = abs(p)-b;return length(max(d,0.))+min(max(d.x,d.y),0.);
}

float rbox( in vec2 p, in vec2 b, in vec4 r ) {
    r.xy = (p.x>0.)?r.xy : r.zw;
    r.x  = (p.y>0.)?r.x  : r.y;
    vec2 q = abs(p)-b+r.x;
    return min(max(q.x,q.y),0.)+length(max(q,0.))-r.x;
}

// Letters and characters
const vec4 bz = vec4(.075,.055,.035,.0);
const vec4 bo = vec4(.175,.15,.075,.025);
const vec2 oa = vec2(.15,.1);

float getdp(vec2 p) {
    float bt = length(p)-.075;
    return bt;
}
// number
float get3(vec2 p) {
    vec2 of = vec2(0,.075);
    float bt = max(
    rbox(p-of,oa,bz.xxww),
    -rbox(p-of+vec2(.05,0),vec2(.15,.05),bz.zzww)
    );
    float bb = max(
    rbox(p+of,oa,bz.xxww),
    -rbox(p+of+vec2(.05,0),vec2(.15,.05),bz.zzww)
    );
    return min(bt,bb);
}
//letters
float getA(vec2 p) {
    vec2 of = vec2(0,.075);
    float bt = max(
    rbox(p,vec2(.15,.175),bz.xwxw),
    -rbox(p+vec2(.0,.05),vec2(.1,.175),bz.zwzw)
    );
    float bb = box(p,vec2(.15,.025));
    return min(bt,bb);
}
float getE(vec2 p) {
    vec2 of = vec2(0,.075);
    float bt = max(
        rbox(p-of,oa,bz.wwxx),
        -rbox(p-of-vec2(.05,0),vec2(.15,.05),bz.wwzz)
    );
    float bb = max(
        rbox(p+of,oa,bz.wwxx),
        -rbox(p+of-vec2(.05,0),vec2(.15,.05),bz.wwzz)
    );
    return min(bt,bb);
}
float getH(vec2 p) {
    float bt = min(
        box(vec2(abs(p.x)-.125,p.y),vec2(.025,.175)),
        box(p,vec2(.15,.025))
    );
    return bt;
}
float getN(vec2 p) {
    float bt = max(
        rbox(p,vec2(.15,.175),bz.xwxw),
        -rbox(p+vec2(.0,.045),vec2(.1,.175),bz.zwzw)
    );
    return bt;
}
float getP(vec2 p) {
    float bt = max(
        rbox(p-vec2(.0,.075),oa,bz.xxww),
        -rbox(p-vec2(.0,.075),vec2(.1,.05),bz.zzww)
    );
    float bb = box(p+vec2(.125,0),vec2(.025,.175));
    return min(bt,bb);
}
float getR(vec2 p) {
    float bt = max(
        rbox(p-vec2(.0,.075),oa,bz.xxww),
        -rbox(p-vec2(.0,.075),vec2(.1,.05),bz.zzww)
    );
    float bb = max(
        rbox(p+vec2(.0,.075),oa,bz.xwww),
        -rbox(p+vec2(.0,.12),vec2(.1,.095),bz.zwww)
    );
    return min(bt,bb);
}
float getW(vec2 p) {
    vec2 of = vec2(.065,0);
    float bt = max(
        rbox(p+of,vec2(.085,.175),bz.wxwx),
        -rbox(p+of-vec2(.0,.05),vec2(.0375,.175),bz.wzwz)
    );
    float bb = max(
    rbox(p-of,vec2(.085,.175),vec4(0,.075,0,.075)),
    -rbox(p-of-vec2(.0,.05),vec2(.0375,.175),bz.wzwz)
    );
    return min(bt,bb);
}
float getY(vec2 p) {
    vec2 of = vec2(0,.075);
    float bt = max(
        rbox(p-of,oa,bz.wxwx),
        -rbox(p-of-vec2(0,.05),vec2(.1),bz.wzwz)
    );
    return min(bt,box(p+of,vec2(.025,.1)));
}

vec2 map(vec3 p) {
    vec2 res = vec2(1e5,0);
    p.y -= 2.;
    vec3 q = p;
    q*=.25;
    
    float amt = 15.;
    float xd = floor((q.z+df)/dz);
    float mx = floor(mod(xd,amt));
    
    float id = floor((q.x+hf)/sz)+xd;
    float md = floor(mod(id,amt));
          sd = vec2(id,xd);
          
    float hs = hash21(sd);
    q.xz= mod(q.xz+vec2(hf,df),vec2(sz,dz))-vec2(hf,df);

    float ff = .6+.3*sin((xd*2.)+id*.475+T);
    float tk = .14+.125*cos(id+md*.25+T*1.15);
    
    q.y -= ff;

    if(hs>.489) q.zy*=rot(ff*PI2);
    
    float d= getdp(q.xy);
    

    if(md==0.) d= getH(q.xy);
    if(md==1.) d= getA(q.xy);
    if(md==2.) d= getP(q.xy);
    if(md==3.) d= getP(q.xy);
    if(md==4.) d= getY(q.xy);
    
    if(md==6.) d= getN(q.xy);
    if(md==7.) d= getE(q.xy);
    if(md==8.) d= getW(q.xy);

    if(md==10.) d= getY(q.xy);
    if(md==11.) d= getE(q.xy);
    if(md==12.) d= getA(q.xy);
    if(md==13.) d= getR(q.xy);

    d=opx(q,d,tk);
    
    if(d<res.x) {
        res=vec2(d/.75,1);
        hit=p;
    }
    
    float mf = .6+.3*sin((p.x*.7)+p.z*.475+T);
    float f = p.y+2.+mf;
    if(f<res.x) {
        res=vec2(f,2);
        hit=p;
    }
    return res;
}

vec3 normal(vec3 p, float t)
{
    float e = MIN_DIST*t;
    vec2 h =vec2(1,-1)*.5773;
    vec3 n = h.xyy * map(p+h.xyy*e).x+
             h.yyx * map(p+h.yyx*e).x+
             h.yxy * map(p+h.yxy*e).x+
             h.xxx * map(p+h.xxx*e).x;
    return normalize(n);
}

vec4 FC = vec4(.06,.27,.44,0);

vec3 hue(float t){ 
    return vec3(.35) + vec3(.25)*cos( PI*(vec3(.95,.92,.88)*t+vec3(.93,.36,.07))); 
}

vec4 render(inout vec3 ro, inout vec3 rd, inout vec3 ref, bool last, inout float d) {

    vec3 C = vec3(0);
    float m = 0.;
    vec3 p = ro;
    
    for(int i=0;i<128;i++) {
        p = ro + rd * d;
        vec2 ray = map(p);
        if(ray.x<MIN_DIST*d||d>MAX_DIST)break;
        d += i<32? ray.x*.75: ray.x;
        m  = ray.y;
    } 
    hp = hit;
    fd = sd;

    if(d<MAX_DIST)
    {
        vec3 n = normal(p,d);
        vec3 lpos =  vec3(22,60,25);
        vec3 l = normalize(lpos);

        float diff = clamp(dot(n,l),0.,1.);
     
        float shdw = 1.;
        for(float t=.01; t < 9.; ) {
            float h = map(p + l*t).x;
            if( h<MIN_DIST ) { shdw = 0.; break; }
            shdw = min(shdw, 64.*h/t);
            t += h;
            if( shdw<MIN_DIST || t>9. ) break;
        }
        diff = mix(diff,diff*shdw,.5);

        vec3 h = vec3(0);

        if(m==1.) {
            h=hue(3.+(fd.x*.1)+fd.y*.2);
            ref=h*.5;
        }
        if(m==2.) {
            h = hue((hp.x*.07)+hp.z*.025);
            ref = vec3(.05);
            vec2 f = fract(hp.xz*.3)-.5;
            if(f.x*f.y>0.) { 
                h = vec3(.55);
                ref=h;
            }
        }

        C = h*diff;

        ro = p+n*MIN_DIST;
        rd = reflect(rd,n);
    }
    return vec4(C,1);
}
 
void main(){
    vec2 F =gl_FragCoord.xy;

    vec2 uv = (2.*F.xy-R.xy)/max(R.x,R.y);

    const float zoom = 11.;

    vec3 ro = vec3(uv*zoom,-(zoom+9.));
    vec3 rd = vec3(0,0,1.);
   
    mat2 rx = rot(-.78);
    mat2 ry = rot(.48);
    ro.yz *= rx,ro.xz *= ry;
    rd.yz *= rx,rd.xz *= ry;

    ro.x+=T*1.13;
    //ro.z-=T;
    vec3 C = vec3(0),ref=vec3(0),fil=vec3(1);
    
    float d = 0.,a = 0.;
    float numBounces = 2.;
    
    for(float i=0.; i<numBounces; i++) {
        d =0.;
        vec4 pass = render(ro, rd, ref, i==numBounces-1., d);
        C += pass.rgb*fil;
        fil*=ref;
        if(i==0.) a = d;
    }

    float tg =hash21(uv);
    if(tg<.5) C = mix(C,C+.07,tg);

    C = mix(FC.rgb,C,exp(-.0001*a*a*a));
    // gamma correction
    C=pow(C, vec3(.4545));
    fragColor = vec4(C,1.);
}

</script>
<!-- partial -->
  <script src='https://jhancock532.github.io/twgl.min.js'></script><script  src="./script.js"></script>

</body>
</html>
